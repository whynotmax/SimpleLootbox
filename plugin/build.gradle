import java.text.SimpleDateFormat

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version "${shadowVersion}"
    id 'net.kyori.blossom' version "${blossomVersion}"
}

java {
    withSourcesJar()
}

mainClassName = 'dev.mzcy.plugin.LootboxPlugin'

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'org.yaml:snakeyaml:2.0'
    implementation 'eu.koboo:en2do:3.1.10'
    implementation 'org.reflections:reflections:0.10.2'

    implementation 'xyz.xenondevs.invui:invui-core:1.43'
    implementation 'xyz.xenondevs.invui:inventory-access-r18:1.43'

    compileOnly "io.papermc.paper:paper-api:${paperVersion}"

    implementation project(":api")
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Implementation-Title': rootProject.name,
                'Implementation-Version': rootProject.version,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

blossom {
    replaceToken 'PROJECT_NAME', rootProject.name
    replaceToken 'PROJECT_VERSION', rootProject.version
}
